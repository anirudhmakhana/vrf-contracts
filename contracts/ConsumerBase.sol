// SPDX-License-Identifier: MIT
pragma solidity 0.8.18;

import {GelatoVRFConsumer} from "contracts/Consumer.sol";
import {GelatoVRFInbox} from "contracts/Inbox.sol";

/// @title GelatoVRFConsumerBase
/// @dev This contract handles domain separation between consecutive randomness requests
abstract contract GelatoVRFConsumerBase is GelatoVRFConsumer {
    uint256 private __requestIdCounter = 1;

    function _inbox() internal view virtual returns (GelatoVRFInbox);

    function _operator() internal view virtual returns (address);

    function _requestRandomness(bytes memory extraData) internal {
        bytes memory data = abi.encode(__requestIdCounter++, extraData);
        _inbox().requestRandomness(this, data);
    }

    function _fulfillRandomness(
        bytes32 seed,
        bytes memory extraData
    ) internal virtual;

    /// @notice Callback function used by Gelato VRF to return the random number.
    /// @param randomness The random number generated by Gelato VRF.
    /// @param data Additional data provided by Gelato VRF, typically containing request details.
    function fulfillRandomness(
        uint256 randomness,
        bytes calldata data
    ) external {
        require(msg.sender == _operator());
        (uint256 requestId, bytes memory extraData) = abi.decode(
            data,
            (uint256, bytes)
        );
        bytes32 seed = keccak256(abi.encode(randomness, requestId));
        _fulfillRandomness(seed, extraData);
    }
}
